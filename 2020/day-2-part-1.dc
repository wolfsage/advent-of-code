# Want:
# low high want pwitem1 [pwitem2...]

1 2 97 98 97 97 98 100 120 97 10
2 6 97 98 97 97 98 97 8

[2Q]sq

# main counter
0sT

# incrementer
[
  lT 1+ sT
]sC

# Main loop
[
  # quit if no items left on stack
  z 0 =q

  # Items on line
  dsm

  # Count of items to look through
  3-sc

  # Loop exiter
  # 
  [
    lm d1- sm # decrement loop counter
    0=q       # quit if we hit 0
    St        # store items in register t
    lLx       # loop
  ]sL

  # do it
  lLx

  # Get low
  Ltsl

  # Get high
  Ltsh

  # Get look for
  Ltsf

  # Count items in register that match what we need
  0sg

  [
    lg 1+ sg
  ]sI

  [
    lc d1- sc # decrement loop counter
    0=q     # quit if we hit 0

    Lt      # grab item to compare and duplicate
    lf =I   # match? increase counter

    lFx
  ]sF

  lFx

  [
    2Q
  ]sn

  [
    lCx # inc!
    2Q
  ]sy

  [
    # less than our low? bad
    ll lg <n

    # higher than our high? bad
    lh lg >n

    lyx
  ]sM

  lMx

  lZx
]sZ

lZx

[total ]n lTp
